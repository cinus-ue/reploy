Target root@105.239.136.127:22

Set V2RAY_VERSION "v4.28.2"
Set PATH "/home/cinus/v2ray"

Task {
    #
    # Remove installed V2Ray.
    #
    Run "sh -c \"[ -d {{PATH}} ] && echo yes \"" When stdout == "yes" Goto CLEAN

    #
    # Create the installation directory: {{PATH}}.
    #
    Run "mkdir {{PATH}}"

    Print "Step 1: Downloading V2Ray"

    #
    # Download V2Ray from GitHub release.
    #
    Run "wget -P {{PATH}} \
        https://github.com/v2ray/v2ray-core/releases/download/{{V2RAY_VERSION}}/v2ray-linux-64.zip"
    When exit_code != "0" Goto DOWNLOAD_FAILED

    Print "Step 2: File decompression"

    #
    # Extract the V2Ray package to {{PATH}}.
    #
    Run "unzip {{PATH}}/v2ray-linux-64.zip \
        -d {{PATH}}"
    When exit_code != "0" Goto DECOMPRESSION_FAILED

    #
    # Upload V2Ray configuration file to {{PATH}}.
    #
    Run "rm -f {{PATH}}/config.json"
    Snd "config.json"  "{{PATH}}/config.json"

    #
    # Used to store V2Ray log files.
    #
    Run "mkdir {{PATH}}/log"

    Print  "Step 3: Starting V2Ray"

    #
    # Start the V2Ray service.
    #
    Run "sh -c \"nohup {{PATH}}/v2ray \
        -c {{PATH}}/config.json >/dev/null 2>&1 &\""
    When exit_code != "0" Goto START_FAILED

    Print "Finished: SUCCESS"
}

Label CLEAN {
    #
    # Remove installed files.
    #
    Run "rm -rf {{PATH}}"
}

Label DOWNLOAD_FAILED {
    Print "Download failed! Please check your network or try again."
    End
}

Label DECOMPRESSION_FAILED {
    Print "V2Ray decompression failed."
    End
}

Label START_FAILED {
    Print "Failed to start V2Ray service."
    End
}

